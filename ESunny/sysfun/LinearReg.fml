//--------------------------------------------------------------
// 简称: LinearReg
// 名称: 求线性回归
// 类型: 用户函数
// 输出: Bool
//--------------------------------------------------------------
Params
	NumericSeries Price;
	Numeric Length;
	Numeric TgtBar;
	NumericRef LRSlope; 
	NumericRef LRAngle;
	NumericRef LRIntercept;
	NumericRef LRValue;
Vars
	Numeric SumXY(0);
	Numeric SumY;
	Numeric SumX; 
	Numeric SumXSqr;
	Numeric Divisor;
	Numeric i;
Begin
	if (Length > 1)
	{
		SumX = Length * ( Length - 1 ) * 1/2;
		SumXSqr = Length * ( Length - 1 ) * ( 2 * Length - 1 ) * 1/6 ;	
		Divisor = Sqr( SumX ) - Length * SumXSqr ;			
		SumY = Summation( Price, Length ) ;
		for i = 0 to Length - 1 
		{
			SumXY = SumXY + i * Price[i] ;
		}
		
		LRSlope = ( Length * SumXY - SumX * SumY) / Divisor ;
		LRAngle = Atan ( LRSlope ) ;
		LRIntercept = ( SumY - LRSlope * SumX ) / Length ;
		LRValue = LRIntercept + (Length - 1 - TgtBar)*LRSlope;
		Return True;
	}Else
	{
		Return False;
	}
End

//--------------------------------------------------------------
// 简称: iEMA
// 名称: iEMA
// 类型: 用户函数
// 输出: Numeric
//--------------------------------------------------------------

Params
	NumericArray Arr;
	Numeric Length(10);
	Numeric Pos(0);  //0：当前周期，如果计算上一周期，Pos为1，依次类推
  
Begin
		
	Integer arrLen = ArrLength(arr);
	if(arrLen<1) Return 0;
	
	if(Pos<0 || Pos>= arrLen ) Return Arr[arrLen-1]; //参数合法性验证
	
	Numeric sFcactor = 2 / ( Length + 1 );  //平滑系数
	
	Integer start = arrLen-1;
	if(start>Pos+200) start=Pos+200;  //最多值计算200个元素，太多没有意义，效率也低
	
	
	Numeric result;
	
	for(Integer i=start;i>=Pos;i--)
	{
		if(i == start)
		{
			result = Arr[i];
		}
		Else
		{
			result = result + sFcactor * (Arr[i] - result);
		}
	}
	
	
	Return result;
End